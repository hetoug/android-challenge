apply plugin: 'com.android.application'

ext {
    supportLibVersion = '25.1.0'
    playLibVersion = '10.0.1'
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE']=code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "dk.adaptmobile.android_seed"
            minSdkVersion 16
            targetSdkVersion 25
            versionCode code
            versionName "1.0"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }



    signingConfigs {
        debug { //SHA1 15:56:88:D5:D2:A0:4D:7D:0C:5A:14:F8:4E:52:32:08:80:02:3B:40
            storeFile file('../../launch/amDebug.keystore')
            storePassword "adaptDebug"
            keyAlias "adaptDebug"
            keyPassword "adaptDebug"
        }
        stage {
            storeFile file('../../launch/CHANGE.keystore')
            storePassword "CHANGE"
            keyAlias "CHANGE"
            keyPassword "CHANGE"
        }
        release {
            storeFile file("../../launch/CHANGE.keystore")
            storePassword "CHANGE"
            keyAlias "CHANGE"
            keyPassword "CHANGE"
        }
    }

    productFlavors {
        dev {
            minSdkVersion 21
        }
        prod {
            minSdkVersion 16
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "AMSeed Dev"
            debuggable true
        }
        stage {
            minifyEnabled false
            applicationIdSuffix ".beta"
            resValue "string", "app_name", "AMSeed Beta"
            debuggable true
            signingConfig signingConfigs.stage
        }
        release {
            minifyEnabled false
            resValue "string", "app_name", "AMSeed"
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.google.android.gms:play-services-base:${playLibVersion}"
    compile 'com.github.adaptmobile-organization:amutil:1.1.8'
    compile 'com.greysonparrelli.permiso:permiso:0.3.0'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    debugCompile 'com.github.adaptmobile-organization:amtestutil:1.0.3'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.2.2'){
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile ('com.android.support.test.espresso:espresso-web:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.4'

    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
}
