apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

ext {
    supportLibVersion = '27.1.0'
    playLibVersion = '12.0.0'
    butterknifeVersion = '8.8.1'
    conductorVersion = '2.1.4'
    firebaseVersion = '12.0.0'
    leakcanaryVersion = '1.5.4'
}
android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "dk.adaptmobile.android_seed"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        debug { //SHA1 15:56:88:D5:D2:A0:4D:7D:0C:5A:14:F8:4E:52:32:08:80:02:3B:40
            storeFile file('../../launch/amDebug.keystore')
            storePassword "adaptDebug"
            keyAlias "adaptDebug"
            keyPassword "adaptDebug"
        }
        stage {
            storeFile file('../../launch/amDebug.keystore')
            storePassword "adaptDebug"
            keyAlias "adaptDebug"
            keyPassword "adaptDebug"
        }
        release {
            storeFile file("../../launch/CHANGE.keystore")
            storePassword "CHANGE"
            keyAlias "CHANGE"
            keyPassword "CHANGE"
        }
    }

    flavorDimensions "default"

    //Performance optimizations
    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("debug")) {
            variant.mergedFlavor.resourceConfigurations.add("en")
            variant.mergedFlavor.resourceConfigurations.add("xxhdpi")
        }
    }

    buildTypes {
        debug {
            //Performance optimizations
            ext.alwaysUpdateBuildId = false // Disable crashlytics build ID on debug
            aaptOptions.cruncherEnabled = false //Disable PNG crunching or use WebP, defaults to false in debug for Android plugin 3.0
            splits.abi.enable = false //Disable ABI splits
            splits.density.enable = false //disable density splits

            minifyEnabled false
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "CHANGE Dev"
            buildConfigField "int", "HTTP_LOGLEVEL", "2"
            buildConfigField "String", "BASE_URL", "\"\""
            debuggable true
        }
        stage {
            minifyEnabled false
            applicationIdSuffix ".stage"
            resValue "string", "app_name", "CHANGE Stage"
            buildConfigField "int", "HTTP_LOGLEVEL", "2"
            buildConfigField "String", "BASE_URL", "\"\""
            debuggable true
            signingConfig signingConfigs.stage
        }
        release {
            minifyEnabled false
            resValue "string", "app_name", "CHANGE"
            buildConfigField "int", "HTTP_LOGLEVEL", "0"
            buildConfigField "String", "BASE_URL", "\"\""
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // --- ANDROID --- //
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-core-utils:$supportLibVersion"

    //KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.github.ajalt:timberkt:1.3.0'

    //CONDUCTOR
    implementation "com.bluelinelabs:conductor:$conductorVersion"
    implementation "com.bluelinelabs:conductor-support:$conductorVersion"

    // --- NETWORK --- //
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    // --- UTIL --- //
//    implementation 'com.github.adaptmobile-organization:amutil:2.0.2'
    implementation 'com.github.adaptmobile-organization:amkotlinutil:2.3.0'
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'com.github.salomonbrys.kodein:kodein-android:4.1.0'
    implementation 'org.parceler:parceler-api:1.1.9'
    kapt 'org.parceler:parceler:1.1.9'
    implementation 'com.greysonparrelli.permiso:permiso:0.3.0'
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // --- PERFORMANCE --- //
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') { transitive = true }
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanaryVersion"
    stageImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanaryVersion"

    // --- UI --- //
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation 'com.pnikosis:materialish-progress:1.7'
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation 'com.androidadvance:topsnackbar:1.1.1'
    implementation 'com.github.bjarkeseverinsen:sweet-alert-dialog:1.4.1'

    // --- FIREBASE --- //
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseVersion"
    implementation "com.google.firebase:firebase-database:$firebaseVersion"
    implementation "com.google.firebase:firebase-storage:$firebaseVersion"
    implementation "com.google.firebase:firebase-messaging:$firebaseVersion"

    // --- TESTING --- //
//    testImplementation 'com.github.adaptmobile-organization:amtestutil:1.0.23'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support', module: 'appcompat-v7' exclude group: 'com.android.support', module: 'support-v4' exclude group: 'com.android.support', module: 'design' exclude module: 'recyclerview-v7'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-web:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}
